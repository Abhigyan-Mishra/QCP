<!-- pass through vertex shader -->
<script id="vertexShader" type="x-shader/x-vertex">
  void main() {
    gl_Position = vec4( position, 1.0 );
  }
</script>

<!-- pass through fragment shader -->
<script id="fragmentShader" type="x-shader/x-fragment">
  uniform vec2 resolution;
  uniform sampler2D texture;

  void main() {
    vec2 uv = gl_FragCoord.xy / resolution.xy;
    vec3 qbit = texture2D( texture, uv ).xyz;
    gl_FragColor = vec4(qbit, 1.0);
  }
</script>
<!-- end pass through shaders -->

<script id="unitaryShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform float bitmask;
  uniform float bitmask2;
  uniform sampler2D texture;
  uniform vec4 vcoeff1, vcoeff2, vcoeff3, vcoeff4;

  vec2 state2xy(float s) {
    vec2 xy;

    xy.x = mod(s, resolution.x);
    xy.y = floor(s / resolution.x);

    return xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;
    float state1 = coord.y * resolution.x + coord.x;
    float state2 = mod(state1, bitmask2);
    vec2 uv1 = coord.xy / resolution.xy;
    vec3 qbit1 = texture2D( texture, uv1 ).xyz;
    vec3 qr;

    if (state2 < bitmask) {
      vec2 uv2 = state2xy(state1 + bitmask) / resolution.xy;
      vec3 qbit2 = texture2D( texture, uv2 ).xyz;
      vec4 iv = vec4(qbit1.r, qbit1.g, qbit2.r, qbit2.g);

      qr.r = dot(iv, vcoeff1);
      qr.g = dot(iv, vcoeff2);
    } else {
      vec2 uv2 = state2xy(state1 - bitmask) / resolution.xy;
      vec3 qbit2 = texture2D( texture, uv2 ).xyz;
      vec4 iv = vec4(qbit2.r, qbit2.g, qbit1.r, qbit1.g);

      qr.r = dot(iv, vcoeff3);
      qr.g = dot(iv, vcoeff4);
    }

    gl_FragColor = vec4(qr, 1.0);
  }
</script>

<script id="swapShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision midiump float;
#endif
  uniform vec2 resolution;
  uniform float upperbitmask1;
  uniform float upperbitmask2;
  uniform float bitmask1;
  uniform float bitmask2;
  uniform sampler2D texture;

  vec2 state2xy(float s) {
    vec2 xy;

    xy.x = mod(s, resolution.x);
    xy.y = floor(s / resolution.x);

    return xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;
    float stateHolder = coord.y * resolution.x + coord.x;
    float state1 = mod(stateHolder, upperbitmask1);
    float state2 = mod(stateHolder, upperbitmask2);
    bool state1Is1 = (mod(floor(stateHolder / bitmask1), 2.0) > 0.5);
    bool state2Is1 = (mod(floor(stateHolder / bitmask2), 2.0) > 0.5);
    bool swap = state1Is1 != state2Is1;
    vec2 uv1 = coord.xy / resolution.xy;
    vec3 qbit1 = texture2D( texture, uv1 ).xyz;
    vec3 qr = qbit1;

    if (swap == true) {
        if (state1 < bitmask1) {
            vec2 uv2 = state2xy(stateHolder + bitmask1 - bitmask2) / resolution.xy;
            vec3 qbit2 = texture2D( texture, uv2 ).xyz;

            qr = qbit2;
        } else {
            vec2 uv2 = state2xy(stateHolder - bitmask1 + bitmask2) / resolution.xy;
            vec3 qbit2 = texture2D( texture, uv2 ).xyz;

            qr = qbit2;
        }
    }

    gl_FragColor = vec4(qr, 1.0);
  }
</script>


<script id="decoherenceShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision midiump float;
#endif
  uniform vec2 resolution;
  uniform sampler2D texture;
  uniform float nrands[22];

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;
    float stateHolder = coord.y * resolution.x + coord.x;
    float angle = 0.0;
    for (int i = 0; i < 22; i++) {
      bool stateIs1 = mod(stateHolder, 2.0) > 0.5;
      stateHolder = floor(stateHolder / 2.0);
      if (stateIs1) {
        angle = angle + nrands[i];
      } else {
        angle = angle - nrands[i];
      }
    }
    vec2 amplitude = vec2(cos(angle), -sin(angle));
    vec2 amplitude2 = vec2(sin(angle), cos(angle));

    vec2 uv1 = coord.xy / resolution.xy;
    vec2 qbit1 = texture2D( texture, uv1 ).xy;

    gl_FragColor = vec4(dot(qbit1, amplitude), dot(qbit1, amplitude2), 0.0, 1.0);
  }
</script>


<script id="toffoliShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform float bitmask;
  uniform float bitmask2;
  uniform float cbitmask1;
  uniform float cbitmask2;
  uniform sampler2D texture;

  vec2 state2xy(float s) {
    vec2 xy;

    xy.x = mod(s, resolution.x);
    xy.y = floor(s / resolution.x);

    return xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;
    float state1 = coord.y * resolution.x + coord.x;
    float state2 = mod(state1, bitmask2);
    bool swap = (mod(floor(state1 / cbitmask1), 2.0) > 0.5) && (mod(floor(state1 / cbitmask2), 2.0) > 0.5);
    vec2 uv1 = coord.xy / resolution.xy;
    vec3 qbit1 = texture2D( texture, uv1 ).xyz;
    vec3 qr = qbit1;

    if (swap == true) {
        if (state2 < bitmask) {
            vec2 uv2 = state2xy(state1 + bitmask) / resolution.xy;
            vec3 qbit2 = texture2D( texture, uv2 ).xyz;

            qr = qbit2;
        } else {
            vec2 uv2 = state2xy(state1 - bitmask) / resolution.xy;
            vec3 qbit2 = texture2D( texture, uv2 ).xyz;

            qr = qbit2;
        }
    }

    gl_FragColor = vec4(qr, 1.0);
  }
</script>

<script id="cphaseShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform float cbitmask1;
  uniform float cbitmask2;
  uniform vec2 shift_re;
  uniform vec2 shift_im;
  uniform sampler2D texture;

  vec2 state2xy(float s) {
      vec2 xy;

      xy.x = mod(s, resolution.x);
      xy.y = floor(s / resolution.x);

      return xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;
    float state1 = coord.y * resolution.x + coord.x;
    bool apply = (mod(floor(state1 / cbitmask1), 2.0) > 0.5) && (mod(floor(state1 / cbitmask2), 2.0) > 0.5);
    vec2 uv = coord.xy / resolution.xy;
    vec2 qbit = texture2D( texture, uv ).xy;
    vec2 qr = qbit;

    if (apply == true) {
      qr.x = dot(qbit, shift_re);
      qr.y = dot(qbit, shift_im);
    }

    gl_FragColor = vec4(qr.x, qr.y, 0.0, 1.0);
  }
</script>

<script id="sigmaxShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform float bitmask;
  uniform float bitmask2;
  uniform sampler2D texture;

  vec2 state2xy(float s) {
    vec2 xy;

    xy.x = mod(s, resolution.x);
    xy.y = floor(s / resolution.x);

    return xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 uv, coord = gl_FragCoord.xy - adjCoord.xy;
    float state1 = coord.y * resolution.x + coord.x;
    float state2 = mod(state1, bitmask2);

    if (state2 < bitmask) {
      uv = state2xy(state1 + bitmask) / resolution.xy;
    } else {
      uv = state2xy(state1 - bitmask) / resolution.xy;
    }

    vec3 qr = texture2D( texture, uv ).xyz;
    gl_FragColor = vec4(qr, 1.0);
  }
</script>

<script id="sigmayShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform float bitmask;
  uniform float bitmask2;
  uniform sampler2D texture;

  vec2 state2xy(float s) {
    vec2 xy;

    xy.x = mod(s, resolution.x);
    xy.y = floor(s / resolution.x);

    return xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 uv, coord = gl_FragCoord.xy - adjCoord.xy;
    float state1 = coord.y * resolution.x + coord.x;
    float state2 = mod(state1, bitmask2);
    vec3 qr;

    if (state2 < bitmask) {
      uv = state2xy(state1 + bitmask) / resolution.xy;
      qr = texture2D( texture, uv ).xyz;
      qr = vec3(qr.g, -qr.r, qr.b);
    } else {
      uv = state2xy(state1 - bitmask) / resolution.xy;
      qr = texture2D( texture, uv ).xyz;
      qr = vec3(-qr.g, qr.r, qr.b);
    }

    gl_FragColor = vec4(qr, 1.0);
  }
</script>

<script id="sigmazShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform float bitmask;
  uniform float bitmask2;
  uniform sampler2D texture;

  vec2 state2xy(float s) {
    vec2 xy;

    xy.x = mod(s, resolution.x);
    xy.y = floor(s / resolution.x);

    return xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;
    float state1 = coord.y * resolution.x + coord.x;
    float state2 = mod(state1, bitmask2);
    vec2 uv = state2xy(state1) / resolution.xy;
    vec3 qr = texture2D( texture, uv ).xyz;

    if (state2 >= bitmask) {
      qr *= -1.0;
    }

    gl_FragColor = vec4(qr, 1.0);
  }
</script>

<script id="bmeasureShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform float bitmask;
  uniform float bitmask2;
  uniform int value;
  uniform sampler2D texture;

  vec2 quickTwoSum(float a, float b) {
    float s = a + b;
    float e = b - (s - a);
    return vec2(s, e);
  }

  vec4 twoSumComp(vec2 ari, vec2 bri) {
    vec2 s = ari + bri;
    vec2 v = s - ari;
    vec2 e = (ari - (s - v)) + (bri - v);
    return vec4(s.x, e.x, s.y, e.y);
  }

  vec2 df64add(vec2 a, vec2 b) {
    vec4 st;
    st = twoSumComp(a, b);
    st.y += st.z;
    st.xy = quickTwoSum(st.x, st.y);
    st.y += st.w;
    st.xy = quickTwoSum(st.x, st.y);
    return st.xy;
  }

  vec2 split(float a ) {
    const float split = 4097.0; // (1 << 12) + 1;
    float t = a * split;
    float ahi = t - (t - a);
    float alo = a - ahi;
    return vec2(ahi, alo);
  }

  vec2 twoProd(float a, float b) {
    float p = a * b ;
    vec2 aS = split(a);
    vec2 bS = split(b);
    float err = ((aS.x * bS.x - p)
        + aS.x * bS.y + aS.y * bS.x)
        + aS.y * bS.y;
    return vec2(p ,err);
  }

  vec2 df64mult(vec2 a, vec2 b) {
    vec2 p ;
    p = twoProd(a.x, b.x);
    p.y += a.x * b.y;
    p.y += a.y * b.x;
    p = quickTwoSum(p.x, p.y);
    return p;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;
    float state1 = coord.y * 2.0 * resolution.x + coord.x * 2.0;
    float state2 = coord.y * 2.0 * resolution.x + coord.x * 2.0 + 1.0;
    float state3 = (coord.y * 2.0 + 1.0) * resolution.x + coord.x * 2.0;
    float state4 = (coord.y * 2.0 + 1.0) * resolution.x + coord.x * 2.0 + 1.0;
    float bit1 = mod(state1, bitmask2);
    float bit2 = mod(state2, bitmask2);
    float bit3 = mod(state3, bitmask2);
    float bit4 = mod(state4, bitmask2);
    vec2 sum = vec2(0.0, 0.0);

    if ((bit1 < bitmask && value == 0) || (bit1 >= bitmask && value != 0)) {
      vec2 uv1 = vec2(coord.x * 2.0, coord.y * 2.0) / resolution.xy;
      vec3 qs = texture2D( texture, uv1 ).xyz;

      sum = df64add(sum, df64mult(vec2(qs.r, 0.0), vec2(qs.r, 0.0)));
      sum = df64add(sum, df64mult(vec2(qs.g, 0.0), vec2(qs.g, 0.0)));
    }
    if ((bit2 < bitmask && value == 0) || (bit2 >= bitmask && value != 0)) {
      vec2 uv2 = vec2(coord.x * 2.0 + 1.0, coord.y * 2.0) / resolution.xy;
      vec3 qs = texture2D( texture, uv2 ).xyz;

      sum = df64add(sum, df64mult(vec2(qs.r, 0.0), vec2(qs.r, 0.0)));
      sum = df64add(sum, df64mult(vec2(qs.g, 0.0), vec2(qs.g, 0.0)));
    }
    if ((bit3 < bitmask && value == 0) || (bit3 >= bitmask && value != 0)) {
      vec2 uv3 = vec2(coord.x * 2.0, coord.y * 2.0 + 1.0) / resolution.xy;
      vec3 qs = texture2D( texture, uv3 ).xyz;

      sum = df64add(sum, df64mult(vec2(qs.r, 0.0), vec2(qs.r, 0.0)));
      sum = df64add(sum, df64mult(vec2(qs.g, 0.0), vec2(qs.g, 0.0)));
    }
    if ((bit4 < bitmask && value == 0) || (bit4 >= bitmask && value != 0)) {
      vec2 uv4 = vec2(coord.x * 2.0 + 1.0, coord.y * 2.0 + 1.0) / resolution.xy;
      vec3 qs = texture2D( texture, uv4 ).xyz;

      sum = df64add(sum, df64mult(vec2(qs.r, 0.0), vec2(qs.r, 0.0)));
      sum = df64add(sum, df64mult(vec2(qs.g, 0.0), vec2(qs.g, 0.0)));
    }

    gl_FragColor = vec4(sum.x, sum.y, 0.0, 1.0);
  }
</script>

<script id="reducingShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform sampler2D texture;

  vec2 quickTwoSum(float a, float b) {
    float s = a + b;
    float e = b - (s - a);
    return vec2(s, e);
  }

  vec4 twoSumComp(vec2 ari, vec2 bri) {
    vec2 s = ari + bri;
    vec2 v = s - ari;
    vec2 e = (ari - (s - v)) + (bri - v);
    return vec4(s.x, e.x, s.y, e.y);
  }

  vec2 df64add(vec2 a, vec2 b) {
    vec4 st;
    st = twoSumComp(a, b);
    st.y += st.z;
    st.xy = quickTwoSum(st.x, st.y);
    st.y += st.w;
    st.xy = quickTwoSum(st.x, st.y);
    return st.xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;

    vec2 uv1 = vec2(coord.x * 2.0, coord.y * 2.0) / resolution.xy;
    vec3 qs = texture2D( texture, uv1 ).xyz;
    vec2 sum = vec2(qs.r, qs.g);

    vec2 uv2 = vec2(coord.x * 2.0 + 1.0, coord.y * 2.0) / resolution.xy;

    qs = texture2D( texture, uv2 ).xyz;
    sum = df64add(sum, vec2(qs.r, qs.g));

    vec2 uv3 = vec2(coord.x * 2.0, coord.y * 2.0 + 1.0) / resolution.xy;

    qs = texture2D( texture, uv3 ).xyz;
    sum = df64add(sum, vec2(qs.r, qs.g));

    vec2 uv4 = vec2(coord.x * 2.0 + 1.0, coord.y * 2.0 + 1.0) / resolution.xy;

    qs = texture2D( texture, uv4 ).xyz;
    sum = df64add(sum, vec2(qs.r, qs.g));

    gl_FragColor = vec4(sum.x, sum.y, 0.0, 1.0);
  }
</script>

<script id="collapseShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform float bitmask;
  uniform float bitmask2;
  uniform int value;
  uniform float norm;
  uniform sampler2D texture;

  vec2 state2xy(float s) {
    vec2 xy;

    xy.x = mod(s, resolution.x);
    xy.y = floor(s / resolution.x);

    return xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;
    float state1 = coord.y * resolution.x + coord.x;
    float bit = mod(state1, bitmask2);
    vec3 qr = vec3(0.0, 0.0, 0.0);

    if (bit < bitmask) {
      if (value != 0)
        state1 += bitmask;

      vec2 uv = state2xy(state1) / resolution.xy;
      vec3 qs = texture2D( texture, uv ).xyz;

      qr = qs * norm;
    }

    gl_FragColor = vec4(qr, 1.0);
  }
</script>

<script id="shiftLeftShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform float bitmask2;
  uniform sampler2D texture;

  vec2 state2xy(float s) {
    vec2 xy;

    xy.x = mod(s, resolution.x);
    xy.y = floor(s / resolution.x);

    return xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;
    float state1 = coord.y * resolution.x + coord.x;
    float bit = floor(mod(state1, bitmask2));
    vec3 qr = vec3(0.0, 0.0, 0.0);

    if (bit == 0.0) {
      vec2 uv = state2xy(state1 / bitmask2) / resolution.xy;

      qr = texture2D( texture, uv ).xyz;
    }

    gl_FragColor = vec4(qr, 1.0);
  }
</script>

<script id="shiftRightShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform float bitmask2;
  uniform float vecmask2;
  uniform sampler2D texture;

  vec2 state2xy(float s) {
    vec2 xy;

    xy.x = mod(s, resolution.x);
    xy.y = floor(s / resolution.x);

    return xy;
  }

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;
    float state1 = (coord.y * resolution.x + coord.x) * bitmask2;

    if (state1 >= vecmask2) {
      gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
      return;
    }

    vec2 uv = state2xy(state1) / resolution.xy;
    vec3 qr = texture2D( texture, uv ).xyz;

    gl_FragColor = vec4(qr, 1.0);
  }
</script>

<script id="floatShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform int selector;
  uniform sampler2D texture;

  vec4 encode_float(float val) {
    if (val == 0.0)
      return vec4(0, 0, 0, 0);
    val = val * 1048576.0;
    float sign = val > 0.0 ? 0.0 : 1.0;
    val = floor(abs(val));

    float byte4 = mod(val, 256.0) / 255.0;
    val = floor(val / 256.0);
    float byte3 = mod(val, 256.0) / 255.0;
    val = floor(val / 256.0);
    float byte2 = mod(val, 256.0) / 255.0;
    float byte1 = sign / 255.0;
    return vec4(byte4, byte3, byte2, byte1);
  }
  void main() {
    vec2 uv = gl_FragCoord.xy / resolution.xy;
    vec4 data = texture2D(texture, uv);

    if (selector == 0) {
        gl_FragColor = encode_float(data.r);
    } else if (selector == 1) {
        gl_FragColor = encode_float(data.g);
    } else {
        gl_FragColor = encode_float(data.b);
    }
  }
</script>

<script id="modulusShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform sampler2D texture;

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;

    vec2 uv1 = vec2(coord.x * 2.0, coord.y * 2.0) / resolution.xy;
    vec2 p1 = texture2D( texture, uv1 ).xy;
    float maxmod = dot(p1, p1);
    vec2 uv2 = vec2(coord.x * 2.0 + 1.0, coord.y * 2.0) / resolution.xy;
    vec2 p2 = texture2D( texture, uv2 ).xy;

    maxmod = max(maxmod, dot(p2, p2));

    vec2 uv3 = vec2(coord.x * 2.0, coord.y * 2.0 + 1.0) / resolution.xy;
    vec2 p3 = texture2D( texture, uv3 ).xy;

    maxmod = max(maxmod, dot(p3, p3));

    vec2 uv4 = vec2(coord.x * 2.0 + 1.0, coord.y * 2.0 + 1.0) / resolution.xy;
    vec2 p4 = texture2D( texture, uv4 ).xy;

    maxmod = max(maxmod, dot(p4, p4));

    gl_FragColor = vec4(maxmod, 0.0, 0.0, 1.0);
  }
  </script>

<script id="maxreduceShader" type="x-shader/x-fragment">
#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif
  uniform vec2 resolution;
  uniform sampler2D texture;

  void main() {
    const vec2 adjCoord = vec2(0.5, 0.5);
    vec2 coord = gl_FragCoord.xy - adjCoord.xy;

    vec2 uv1 = vec2(coord.x * 2.0, coord.y * 2.0) / resolution.xy;
    float maxmod = texture2D( texture, uv1 ).x;
    vec2 uv2 = vec2(coord.x * 2.0 + 1.0, coord.y * 2.0) / resolution.xy;

    maxmod = max(maxmod, texture2D( texture, uv2 ).x);

    vec2 uv3 = vec2(coord.x * 2.0, coord.y * 2.0 + 1.0) / resolution.xy;

    maxmod = max(maxmod, texture2D( texture, uv3 ).x);

    vec2 uv4 = vec2(coord.x * 2.0 + 1.0, coord.y * 2.0 + 1.0) / resolution.xy;

    maxmod = max(maxmod, texture2D( texture, uv4 ).x);

    gl_FragColor = vec4(maxmod, 0.0, 0.0, 1.0);
  }
</script>

<script id="renderShader" type="x-shader/x-vertex">
  varying vec2 vUv;

  void main() {
    vUv = uv;
    vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
    gl_Position = projectionMatrix * mvPosition;
  }
</script>

<script id="render3dShader" type="x-shader/x-vertex">
  uniform float norm;
  uniform sampler2D texture;
  varying vec4 color;
  float grid2 = 32.0 - 32.0 / 130.0;
  float grid1 = -grid2;
  float grid3 = 1.0 / (1.0 - 2.0 / 130.0);
  float grid4 = 1.0 / 130.0;
  float PI = atan(1.0) * 4.0;

  void main() {
    vec2 vUv = clamp((uv - vec2(grid4, grid4)) * grid3, vec2(0.0, 0.0), vec2(1.0, 1.0));
    float dy = 0.0;
    vec3 c = vec3(0.3, 0.45, 0.3);

    if (position.x > grid1 && position.x < grid2 && position.y > grid1 && position.y < grid2) {
      vec3 qr = texture2D(texture, vUv).xyz;
      dy = clamp((qr.r * qr.r + qr.g * qr.g) * norm, 0.0, 1.0) * 40.0;
      c = vec3(0.25, 0.25, 0.25);

      if (dy != 0.0) {
        float a = atan(qr.g, qr.r) / PI * 2.0;

        if (a > 0.0) {
          if (a < 1.0) {
            c = vec3(1.0, 1.0, 1.0) * (1.0 - a) + vec3(0.0, 1.0, 0.0) * a;
          } else {
            c = vec3(0.0, 1.0, 0.0) * (2.0 - a) + vec3(0.0, 1.0, 1.0) * (a - 1.0);
          }
        } else {
          if (a > -1.0) {
            c = vec3(1.0, 1.0, 1.0) * (1.0 + a) + vec3(-1.0, 0.0, 0.0) * a;
          } else {
            c = vec3(1.0, 0.0, 0.0) * (2.0 + a) + vec3(0.0, -1.0, -1.0) * (a + 1.0);
          }
        }
      }
    }
    vec4 mvPosition = modelViewMatrix * vec4( position + vec3(0.0, 0.0, dy), 1.0 );
    gl_Position = projectionMatrix * mvPosition;

    float s = clamp(0.5 + normalize(vec3(30, 50, 50) - gl_Position.xyz).y * 0.6, 0.0, 1.0);

    color = vec4(s * c, 1.0);
  }
</script>

<script id="render3dShader2" type="x-shader/x-vertex">
  uniform float norm;
  uniform sampler2D texture;
  varying vec4 color;
  float grid2 = 32.0 - 32.0 / 130.0;
  float grid1 = -grid2;
  float grid3 = 1.0 / (1.0 - 2.0 / 130.0);
  float grid4 = 1.0 / 130.0;
  float PI = atan(1.0) * 4.0;

  void main() {
    vec2 vUv = clamp((uv - vec2(grid4, grid4)) * grid3, vec2(0.0, 0.0), vec2(1.0, 1.0));
    float dy = 0.0;
    vec3 c = vec3(0.3, 0.45, 0.3);

    if (position.x > grid1 && position.x < grid2 && position.y > grid1 && position.y < grid2) {
      vec3 qr = texture2D(texture, vUv).xyz;
      dy = clamp((qr.r * qr.r + qr.g * qr.g) * norm, 0.0, 1.0) * 40.0;
      c = vec3(0.25, 0.25, 0.25);

      if (dy != 0.0) {
        float a = atan(qr.g, qr.r) / PI * 2.0;

        if (a > 0.0) {
          if (a < 1.0) {
            c = vec3(1.0, 1.0, 1.0) * (1.0 - a) + vec3(0.1, 0.6, 1.0) * a;
          } else {
            c = vec3(0.1, 0.6, 1.0) * (2.0 - a) + vec3(1.0, 1.0, 1.0) * (a - 1.0);
          }
        } else {
          if (a > -1.0) {
            c = vec3(1.0, 1.0, 1.0) * (1.0 + a) + vec3(-1.0, -0.2, -0.2) * a;
          } else {
            c = vec3(1.0, 0.2, 0.2) * (2.0 + a) + vec3(-1.0, -1.0, -1.0) * (a + 1.0);
          }
        }
      }
    }
    vec4 mvPosition = modelViewMatrix * vec4( position + vec3(0.0, 0.0, dy), 1.0 );
    gl_Position = projectionMatrix * mvPosition;

    float s = clamp(0.2 + normalize(vec3(30, 50, 50) - gl_Position.xyz).y * 0.9, 0.0, 1.0);

    color = vec4(s * c, 1.0);
  }
</script>

<script id="colorShader" type="x-shader/x-fragment">
  uniform float norm;
  uniform sampler2D texture;
  varying vec2 vUv;

  void main() {
    vec3 qr = texture2D( texture, vUv ).xyz;

    float y = clamp((qr.r * qr.r + qr.g * qr.g) * norm, 0.0, 1.0);

    gl_FragColor = vec4(y, y, y == 0.0 ? 0.35 : y, 1.0);
  }
</script>

<script id="colorPhaseShader" type="x-shader/x-fragment">
  uniform float norm;
  uniform sampler2D texture;
  varying vec2 vUv;
  float PI = atan(1.0) * 4.0;

  void main() {
    vec3 qr = texture2D( texture, vUv ).xyz;

    float y = clamp((qr.r * qr.r + qr.g * qr.g) * norm, 0.0, 1.0);
    float a = atan(qr.g, qr.r) / PI * 2.0;
    vec3 c = vec3(0.0, 0.0, 0.0);

    if (y > 0.01 && a > 0.0) {
      if (a < 1.0) {
        c = vec3(1.0, 1.0, 1.0) * (1.0 - a) + vec3(0.0, 1.0, 0.0) * a;
      } else {
        c = vec3(0.0, 1.0, 0.0) * (2.0 - a) + vec3(0.0, 1.0, 1.0) * (a - 1.0);
      }
    } else {
      if (a > -1.0) {
        c = vec3(1.0, 1.0, 1.0) * (1.0 + a) + vec3(-1.0, 0.0, 0.0) * a;
      } else {
        c = vec3(1.0, 0.0, 0.0) * (2.0 + a) + vec3(0.0, -1.0, -1.0) * (a + 1.0);
      }
    }

    gl_FragColor = vec4(c * y, 1.0);
  }
</script>

<script id="color3dShader" type="x-shader/x-fragment">
  varying vec4 color;

  void main() {
    gl_FragColor = color;
  }
</script>
